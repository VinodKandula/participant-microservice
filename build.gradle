buildscript {
	ext {
		NEXUS_REPO_URL = 'http://192.168.1.31:8082/nexus/content/repositories'

		springBootVersion = '1.4.0.RELEASE'
		sshVersion = '2.2.0'
		gradleReleaseVersion = '2.4.0'

		axonVersion = '2.4.5'
		commonsLangVersion = '3.4'
		commonsValidatorVersion = '1.5.1'
		jugVersion = '3.1.4'
		mongodbJavaDriverVersion = '2.14.3'

		logbackVersion = '1.1.6'
		logstashLogbackVersion = '4.7'
		janinoVersion = '3.0.1'
		slf4jVersion = '1.7.21'

		springBootAdminClientVersion = '1.4.1'
		jolokiaVersion = '1.3.4'

		springfoxSwagger2Version = '2.5.0'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.hidetake:gradle-ssh-plugin:${sshVersion}")
		classpath("net.researchgate:gradle-release:${gradleReleaseVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'net.researchgate.release'
apply plugin: "jacoco"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-actuator-docs')
	compile('org.springframework.boot:spring-boot-starter-aop')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-starter-data-cassandra')
	compile('org.springframework.boot:spring-boot-starter-web')

	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')

	compile('org.springframework.retry:spring-retry')

	compile ("org.axonframework:axon-core:${axonVersion}")
	compile ("org.axonframework:axon-mongo:${axonVersion}")
	compile ("org.axonframework:axon-amqp:${axonVersion}")
	compile ("org.axonframework:axon-test:${axonVersion}")

	compile ("ch.qos.logback:logback-classic:${logbackVersion}")
	compile ("ch.qos.logback:logback-core:${logbackVersion}")
	compile ("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	compile ("org.codehaus.janino:janino:${janinoVersion}")
	compile ("org.slf4j:slf4j-api:${slf4jVersion}")

	compile ("org.apache.commons:commons-lang3:${commonsLangVersion}")
	compile ("commons-validator:commons-validator:${commonsValidatorVersion}")
	compile ("com.fasterxml.uuid:java-uuid-generator:${jugVersion}")

	compile ("org.mongodb:mongo-java-driver:${mongodbJavaDriverVersion}")

	compile ("de.codecentric:spring-boot-admin-starter-client:${springBootAdminClientVersion}")
	compile ("org.jolokia:jolokia-core:${jolokiaVersion}")

	compile ("io.springfox:springfox-swagger2:${springfoxSwagger2Version}")
	compile ("io.springfox:springfox-swagger-ui:${springfoxSwagger2Version}")

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.SR1"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}

// Test
apply from: 'test.gradle'

// Publish to Nexus
apply from: 'publish.gradle'

// Deploy
apply from: 'deploy.gradle'

// Create a fully executable jar
springBoot {
	executable = true

	embeddedLaunchScriptProperties = [initInfoDescription: project.description,
									  initInfoShortDescription: project.name,
									  initInfoProvides: jar.baseName,
									  mode: 'service']
}

// Make the System.properties available in the bootRun task.
bootRun.systemProperties = System.properties

// Automatically expand info properties
processResources {
	filesMatching('**/application.yml') { expand(project.properties) }
}

ext.jenkinsNode = System.env.NODE_NAME
ext.jenkinsJobName = System.env.JOB_NAME
ext.jenkinsBuildTimestamp = System.env.BUILD_TIMESTAMP
ext.jenkinsBuildId = System.env.BUILD_ID
ext.jenkinsBuildNumber = System.env.BUILD_NUMBER
ext.jenkinsGitCommit = System.env.GIT_COMMIT_ID // Custom env variable set in the Jenkinsfile
ext.jenkinsGitBranch = System.env.BRANCH_NAME